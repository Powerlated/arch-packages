# Contributor: Sebastian Meyer <mail@bastimeyer.de>
# Contributor: Dongsheng Cai <imdongsheng@gmail.com>
# Maintainer: Adria Arrufat (swiftscythe) <swiftscythe@gmail.com>

_pkgname=gthumb
pkgname=gthumb-git
pkgver=3.6.0.7.gb0c1d5dc
pkgrel=1
pkgdesc="Image browser and viewer for the GNOME Desktop"
arch=(i686 x86_64)
url="http://live.gnome.org/gthumb/"
license=('GPL')
provides=($_pkgname=$pkgver)
conflicts=($_pkgname)
depends=(
    clutter-gtk
    dconf
    desktop-file-utils
    gsettings-desktop-schemas
    gst-plugins-base-libs
    hicolor-icon-theme
    librsvg
    libwebp
)
makedepends=(
    docbook-xsl
    exiv2
    gnome-common
    intltool
    itstool
    libraw
    librsvg
    yelp-tools
)
optdepends=(
    'exiv2: metadata support'
    'libraw: read RAW files'
)
options=('!emptydirs')
install=gthumb.install
source=($_pkgname::git+https://git.gnome.org/browse/$_pkgname)
md5sums=('SKIP')

pkgver() {
    cd $_pkgname/
    git describe | sed 's/^v//;s/-/./g'
}

prepare() {
    cd $_pkgname/
    patch -p1 < "${startdir}/0000-disable-web-albums.patch"
    patch -p1 < "${startdir}/0000-zoom-fixes.patch"
}

build() {
    cd $_pkgname/
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static \
        --disable-libbrasero \
        --disable-libjson-glib \
        --disable-libchamplain \
        --disable-libsecret \
        --disable-libsoup \
        --disable-webkit2 \
        --enable-libraw \
        --enable-librsvg
    make
}

package() {
    cd $_pkgname/
    make DESTDIR="$pkgdir" install
    install -D -m644 $srcdir/gthumb/data/icons/hicolor/48x48/apps/gthumb.png $pkgdir/usr/share/pixmaps/gthumb.png
}
